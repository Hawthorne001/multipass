# Copyright (C) Canonical, Ltd.
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License version 3 as
# published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

set_target_properties(dart_ffi PROPERTIES WINDOWS_EXPORT_ALL_SYMBOLS ON)
install(TARGETS dart_ffi DESTINATION bin/ COMPONENT desktop_gui)

find_program(POWERSHELL_PATH NAMES powershell)
set(DESKTOP_GUI_EXECUTABLE desktop_gui.exe)
set(DESKTOP_GUI_TARGET windows)
set(DESKTOP_GUI_PREFIX ${DESKTOP_GUI_TARGET}/runner/Release)
set(DESKTOP_GUI_BUNDLE_RELATIVE "")
set(DESKTOP_GUI_EXEC_PATH_RELATIVE_TO_BUNDLE "")
set(PROTOC_DART_PLUGIN_PATH "$ENV{USERPROFILE}\\AppData\\Local\\Pub\\Cache\\bin\\protoc-gen-dart.bat")

set(DESKTOP_GUI_BUILD_DIR ${CMAKE_RUNTIME_OUTPUT_DIRECTORY})
cmake_path(RELATIVE_PATH DESKTOP_GUI_BUILD_DIR)

if (DESKTOP_GUI_BUILD_DIR STREQUAL "")
  message(FATAL_ERROR "Build and source folders should be in the same drive")
endif()

set(DESKTOP_GUI_PATH ${DESKTOP_GUI_BUILD_DIR}/${DESKTOP_GUI_PREFIX})
set(DESKTOP_GUI_BUNDLE ${DESKTOP_GUI_PATH}/${DESKTOP_GUI_BUNDLE_RELATIVE})
set(DESKTOP_GUI_SOURCE_DIR ${CMAKE_SOURCE_DIR}/src/client/desktop_gui)
set(DESKTOP_GUI_LINK_NAME multipass.desktop-gui)
set(DESKTOP_GUI_EXECUTABLE_FULL_PATH ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${DESKTOP_GUI_PREFIX}/${DESKTOP_GUI_BUNDLE_RELATIVE}/${DESKTOP_GUI_EXEC_PATH_RELATIVE_TO_BUNDLE}/${DESKTOP_GUI_EXECUTABLE})

file(GLOB DESKTOP_GUI_SOURCE_FILES ${DESKTOP_GUI_SOURCE_DIR}/lib/*.dart)

set(FLUTTER_BUILD_ARGS " --release --suppress-analytics --verbose")

add_custom_command(OUTPUT ${DESKTOP_GUI_EXECUTABLE_FULL_PATH}
  DEPENDS ${DESKTOP_GUI_SOURCE_FILES}
  WORKING_DIRECTORY ${DESKTOP_GUI_SOURCE_DIR}
  COMMAND ${CMAKE_COMMAND} -E make_directory lib/generated
  COMMAND ${POWERSHELL_PATH} -Command "pwd ; $$env:PATH += \"\"\";C:\\Program\ Files\\Git\\cmd;C:\\tools\\flutter\\bin;C:\\tools\\flutter\\bin\\mingit\\cmd;$ENV{USERPROFILE}\\AppData\\Local\\Pub\\Cache\\bin\"\"\" ; cd ${DESKTOP_GUI_SOURCE_DIR} ; pwd ; ${DART_EXECUTABLE} --verbose pub global activate protoc_plugin ; $<TARGET_FILE:protobuf::protoc> --plugin=protoc-gen-dart=${PROTOC_DART_PLUGIN_PATH} --dart_out=grpc:lib/generated -I ${grpc_SOURCE_DIR}/third_party/protobuf/src -I ../../rpc ../../rpc/multipass.proto google/protobuf/timestamp.proto ; ${FLUTTER_EXECUTABLE} config --build-dir=${DESKTOP_GUI_BUILD_DIR} ; ${FLUTTER_EXECUTABLE} build ${DESKTOP_GUI_TARGET} ${FLUTTER_BUILD_ARGS} ; ${FLUTTER_EXECUTABLE} config --build-dir="
)

add_custom_target(${DESKTOP_GUI_EXECUTABLE} ALL
  DEPENDS ${DESKTOP_GUI_EXECUTABLE_FULL_PATH}
)

set(FLUTTER_INSTALL_BUNDLE "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${DESKTOP_GUI_PREFIX}/${DESKTOP_GUI_BUNDLE_RELATIVE}/${DESKTOP_GUI_EXEC_PATH_RELATIVE_TO_BUNDLE}/")

install(DIRECTORY
  ${FLUTTER_INSTALL_BUNDLE}
  DESTINATION bin/
  COMPONENT desktop_gui
)
