# Copyright (C) Canonical, Ltd.
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License version 3 as
# published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

set(DESKTOP_GUI_TARGET macos)
set(DESKTOP_GUI_PREFIX macos/Build/Products/Release)
set(DESKTOP_GUI_BUNDLE_RELATIVE "desktop_gui.app")
set(DESKTOP_GUI_EXEC_PATH_RELATIVE_TO_BUNDLE Contents/MacOS)
set(DESKTOP_GUI_EXECUTABLE desktop_gui)
set(PROTOC_DART_PLUGIN_PATH $ENV{HOME}/.pub-cache/bin/protoc-gen-dart)

set(DESKTOP_GUI_BUILD_DIR ${CMAKE_RUNTIME_OUTPUT_DIRECTORY})
cmake_path(RELATIVE_PATH DESKTOP_GUI_BUILD_DIR)

set(DESKTOP_GUI_PATH ${DESKTOP_GUI_BUILD_DIR}/${DESKTOP_GUI_PREFIX})
set(DESKTOP_GUI_BUNDLE ${DESKTOP_GUI_PATH}/${DESKTOP_GUI_BUNDLE_RELATIVE})
set(DESKTOP_GUI_SOURCE_DIR ${CMAKE_SOURCE_DIR}/src/client/desktop_gui)
set(DESKTOP_GUI_LINK_NAME multipass.desktop-gui)
set(DESKTOP_GUI_EXECUTABLE_FULL_PATH ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${DESKTOP_GUI_PREFIX}/${DESKTOP_GUI_BUNDLE_RELATIVE}/${DESKTOP_GUI_EXEC_PATH_RELATIVE_TO_BUNDLE}/${DESKTOP_GUI_EXECUTABLE})

file(GLOB DESKTOP_GUI_SOURCE_FILES ${DESKTOP_GUI_SOURCE_DIR}/lib/*.dart)

set(FLUTTER_BUILD_ARGS --release --suppress-analytics --verbose --no-tree-shake-icons)

add_custom_command(OUTPUT ${DESKTOP_GUI_EXECUTABLE_FULL_PATH}
  DEPENDS ${DESKTOP_GUI_SOURCE_FILES}
  WORKING_DIRECTORY ${DESKTOP_GUI_SOURCE_DIR}
  COMMAND ${CMAKE_COMMAND} -E make_directory lib/generated
  COMMAND ${DART_EXECUTABLE} pub global activate protoc_plugin
  COMMAND $<TARGET_FILE:protoc> --plugin=protoc-gen-dart=${PROTOC_DART_PLUGIN_PATH} --dart_out=grpc:lib/generated -I ${grpc_SOURCE_DIR}/third_party/protobuf/src -I ../../rpc ../../rpc/multipass.proto google/protobuf/timestamp.proto
  COMMAND ${FLUTTER_EXECUTABLE} config --build-dir=${DESKTOP_GUI_BUILD_DIR}
  COMMAND ${FLUTTER_EXECUTABLE} ARGS build ${DESKTOP_GUI_TARGET} ${FLUTTER_BUILD_ARGS}
  COMMAND ${FLUTTER_EXECUTABLE} config --build-dir=
)

add_custom_target(${DESKTOP_GUI_EXECUTABLE} ALL
  DEPENDS ${DESKTOP_GUI_EXECUTABLE_FULL_PATH}
)

install(DIRECTORY ${DESKTOP_GUI_BUNDLE}/Contents
  DESTINATION "bin/Multipass Desktop.app/"
  USE_SOURCE_PERMISSIONS
  COMPONENT desktop_gui
  PATTERN "*/PkgInfo" EXCLUDE
  PATTERN "*/_CodeSignature*" EXCLUDE
)

set_target_properties( ${DESKTOP_GUI_EXECUTABLE} PROPERTIES
  MACOSX_BUNDLE TRUE
  INSTALL_RPATH "@executable_path/../Frameworks"
)

add_custom_target(bundle_symlink ALL
  WORKING_DIRECTORY ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}
  COMMAND ${CMAKE_COMMAND} -E create_symlink ${DESKTOP_GUI_PREFIX}/${DESKTOP_GUI_BUNDLE_RELATIVE} "Multipass Desktop.app"
)
